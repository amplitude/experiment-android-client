plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}


group = ARTIFACT_GROUP
version = ARTIFACT_VERSION

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionName ARTIFACT_VERSION

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}



afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                artifactId = ARTIFACT_ID
                from components.release
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.release)
    }
}

dependencies {
    implementation 'com.amplitude:android-sdk:2.26.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okio:okio:2.10.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20201115'
}

task docs(type: Javadoc) {
    description "Generates Javadoc"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += project.files('build/generated/source/buildConfig/release')
    options.docTitle = "Experiment Android SDK v$version API"
    options.windowTitle = "Experiment Android SDK v$version API"
    destinationDir = file("../docs/")

    options {
        encoding = 'UTF-8'
        docEncoding = 'UTF-8'
        charSet = 'UTF-8'
    }
}

afterEvaluate {
    docs.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
}
